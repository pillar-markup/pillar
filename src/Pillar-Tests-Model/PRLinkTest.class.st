Class {
	#name : #PRLinkTest,
	#superclass : #PRReferenceTest,
	#category : 'Pillar-Tests-Model-Document'
}

{ #category : #testing }
PRLinkTest class >> isAbstract [
	^ true
]

{ #category : #private }
PRLinkTest >> actualClass [
	^ PRLink
]

{ #category : #private }
PRLinkTest >> markup [
	^ PRPillarParser markups at: PRLink
]

{ #category : #private }
PRLinkTest >> referenceString [
	self subclassResponsibility
]

{ #category : #tests }
PRLinkTest >> testIsBroken [
	self assert: (self instance isBroken or: [ self instance isBroken not ])
]

{ #category : #tests }
PRLinkTest >> testIsExternal [
	self deny: self instance isExternal
]

{ #category : #tests }
PRLinkTest >> testIsInternal [
	self deny: self instance isInternal
]

{ #category : #tests }
PRLinkTest >> testIsLink [
	self assert: self instance isLink
]

{ #category : #'tests-parsing' }
PRLinkTest >> testParseAliased [
	| wiki text tree |
	wiki := '*Alias>' , self referenceString , '*'.
	text := 'Alias'.
	tree := PRDocument new
		add: (PRParagraph new
			add: (self actualClass new
				add: (PRText content: 'Alias');
				reference: self referenceString;
				yourself);
			yourself);
		yourself.
	self assertWiki: wiki equalsTree: tree.
	self assertTree: tree equalsWiki: wiki.
	self assertTree: tree equalsText: text
]

{ #category : #'tests-parsing' }
PRLinkTest >> testParseAliasedEmpty [
	| wiki text tree |
	wiki := '*>' , self referenceString , '*'.
	text := ''.
	tree := PRDocument new
		add: (PRParagraph new
			add: (self actualClass new
				add: (PRText content: '');
				reference: self referenceString;
				yourself);
			yourself);
		yourself.
	self assertWiki: wiki equalsTree: tree.
	self assertTree: tree equalsWiki: wiki.
	self assertTree: tree equalsText: text
]

{ #category : #'tests-parsing' }
PRLinkTest >> testParseBasic [
	| wiki text tree |
	wiki := '*' , self referenceString , '*'.
	text := self actualClass new
		reference: self referenceString;
		title.
	tree := PRDocument new
		add: (PRParagraph new
			add: (self actualClass new
				reference: self referenceString;
				yourself);
			yourself);
		yourself.
	self assertWiki: wiki equalsTree: tree.
	self assertTree: tree equalsWiki: wiki.
	self assertTree: tree equalsText: text
]

{ #category : #'tests-parsing' }
PRLinkTest >> testParseParameters [
	| wiki text tree |
	wiki := '*Alias>' , self referenceString , '|a=1|b=2|c*'.
	text := 'Alias'.
	tree := PRDocument new
		add:
				(PRParagraph new
						add:
								(self actualClass new
										add: (PRText content: 'Alias');
										reference: self referenceString;
										parameters:
												(OrderedDictionary new
														add: 'a' -> '1';
														add: 'b' -> '2';
														add: 'c' -> nil;
														yourself);
										yourself);
						yourself);
		yourself.
	self assertWiki: wiki equalsTree: tree.
	self assertTree: tree equalsWiki: wiki.
	self assertTree: tree equalsText: text
]

{ #category : #'tests-parsing' }
PRLinkTest >> testParseParametersEmpty [
	| wiki text tree |
	wiki := '*Alias>' , self referenceString , '|a||b|*'.
	text := 'Alias'.
	tree := PRDocument new
		add:
				(PRParagraph new
						add:
								(self actualClass new
										add: (PRText content: 'Alias');
										reference: self referenceString;
										parameters:
												(OrderedDictionary new
														add: 'a' -> nil;
														add: 'b' -> nil;
														yourself);
										yourself);
						yourself);
		yourself.
	self assertWiki: wiki equalsTree: tree.
	self assertTree: tree equalsText: text
]

{ #category : #'tests-parsing' }
PRLinkTest >> testParseParametersEscaped [
	| wiki text tree |
	wiki := '*Alias>' , self referenceString , '|2=\|\=|\|\==1*'.
	text := 'Alias'.
	tree := PRDocument new
		add:
				(PRParagraph new
						add:
								(self actualClass new
										add: (PRText content: 'Alias');
										reference: self referenceString;
										parameters:
												(OrderedDictionary new
														add: '2' -> '|=';
														add: '|=' -> '1';
														yourself);
										yourself);
						yourself);
		yourself.
	self assertWiki: wiki equalsTree: tree.
	self assertTree: tree equalsWiki: wiki.
	self assertTree: tree equalsText: text
]
